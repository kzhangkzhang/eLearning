function render_apex_jqgrid (
   p_region              in apex_plugin.t_region,
   p_plugin              in apex_plugin.t_plugin,
   p_is_printer_friendly in boolean
)

   return apex_plugin.t_region_render_result

is

   l_render_result apex_plugin.t_region_render_result;
   l_onload_code   varchar2(4000);
   l_crlf          char(2) := chr(13)||chr(10);
   l_col_idx       pls_integer := 0;
   
begin

   if apex_application.g_debug 
   then
      apex_plugin_util.debug_region (
         p_plugin => p_plugin,
         p_region => p_region
      );
   end if;

   sys.htp.p('<table id="' || p_region.static_id || '_APEX_JQ_GRID"></table>');
   sys.htp.p('<div id="' || p_region.static_id || '_APEX_JQ_GRID_PAGER"></div>');

   l_onload_code := 'apex.jQuery("#' || p_region.static_id || '").apex_jqgrid({' || l_crlf ||
      '   ' || apex_javascript.add_attribute('ajaxIdentifier', apex_plugin.get_ajax_identifier(), false, true) || l_crlf ||
      '   columns: [' || l_crlf;

   for x in 1 .. p_region.region_columns.count
   loop
      if p_region.region_columns(x).is_displayed
      then
         l_col_idx := l_col_idx + 1;

         if l_col_idx > 1
         then
            l_onload_code := l_onload_code || ',' || l_crlf;
         end if;

         l_onload_code := l_onload_code ||
            '      {' || l_crlf ||
            '         ' || apex_javascript.add_attribute('heading', p_region.region_columns(l_col_idx).heading, false, true) || l_crlf ||
            '         ' || apex_javascript.add_attribute('frozen', p_region.region_columns(l_col_idx).attribute_01, false, false) || l_crlf ||
            '      }';
      end if;
   end loop;

   l_onload_code := l_onload_code || l_crlf ||
      '   ]' || l_crlf ||
      '});';
      
   apex_javascript.add_onload_code (
      p_code => l_onload_code
   );
   
   return l_render_result;
   
end render_apex_jqgrid;

function ajax_apex_jqgrid (
   p_region in apex_plugin.t_region,
   p_plugin in apex_plugin.t_plugin
)

   return apex_plugin.t_region_ajax_result

is

   l_ajax_result       apex_plugin.t_region_ajax_result;
   l_column_value_list apex_plugin_util.t_column_value_list;

begin

   l_column_value_list :=
      apex_plugin_util.get_data(
         p_sql_statement    => p_region.source,
         p_min_columns      => 2,
         p_max_columns      => 50,
         p_component_name   => p_region.name
   );

   apex_json.open_array;

   for row_idx in 1 .. l_column_value_list(1).count
   loop
      apex_json.open_array;

      for column_idx in 1 .. l_column_value_list.count
      loop
         if p_region.region_columns(column_idx).is_displayed
         then
            if p_region.region_columns(column_idx).escape_output
            then
               apex_json.write(apex_escape.html(l_column_value_list(column_idx)(row_idx)));
            else
               apex_json.write(l_column_value_list(column_idx)(row_idx));
            end if;
         end if;
      end loop;

      apex_json.close_array;
   end loop;

   apex_json.close_array;

   return l_ajax_result;

end ajax_apex_jqgrid;
